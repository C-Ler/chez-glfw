(import (rnrs (6))
        (srfi :48)
        (parsing xml)
        (gl-ffi-gen types)
        (gl-ffi-gen enums)
        (gl-ffi-gen features)
        (gl-ffi-gen commands)
        (gl-ffi-gen private utility)
        (gl-ffi-gen private cut)
        (chez-test assertions))

(define (alist? lst)
  (and (list? lst)
       (all? pair? lst)))

(define-syntax log-do
  (syntax-rules ()
    ((_ expr ...)
     (begin (display '(expr ...)) (newline)
            (expr ...)))))

(define registry
  (document-root (xml:read "registry/gl.xml")))

(define (test-types)
  (let* ((types    (get-types registry)))
    (assert-predicate alist? types)
    (assert-all (lambda (p)
                  (and (string? (car p))
                       (symbol? (cdr p))))
                types)))

(define (test-enums)
  (let* ((enums    (get-enums registry)))
    (assert-predicate hashtable? enums)
    (assert-equal (enum-value (hashtable-ref enums "GL_FALSE" #f)) 0)
    (assert-equal (enum-value (hashtable-ref enums "GL_TRUE" #f)) 1)))

(define (test-features)
  (let* ((features (get-features registry)))
    (assert-predicate list? features)
    (assert-all feature? features)))

(define (test-commands)
  (let* ((types    (get-types registry))
         (commands (get-commands registry types)))
    (assert-predicate hashtable? commands)
    (assert-all command? (map (cut hashtable-ref commands <> #f)
                              (vector->list (hashtable-keys commands))))
  ))
